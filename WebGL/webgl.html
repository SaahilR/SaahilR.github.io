<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8" />
    <title>WebGL_Tutorial</title>
    <link href="../css/default.css" rel="stylesheet" />
    <script src="../js/gl-matrix.js"></script>
    <script src="../js/shader.js"></script>
    <script src="../js/camera.js"></script>
    <script src="../js/texture.js"></script>
</head>
<body>
    <h2>Example WebGL Application</h2>
    Controls:
    <p>
        WASD - Move<br />
        Mouse - Look<br />
    </p>

    <canvas id="renderCanvas" width="960" height="540"></canvas>
    <script src="../js/main.js"></script>

    <script id="mainVertShader" type="vertShader">
        #version 300 es

        in vec3 aPosition;
        in vec3 aNormal;
        in vec2 aTexCoords;

        out vec3 fragPos;
        out vec2 texCoords;
        out vec3 TangentLightPos;
        out vec3 TangentViewPos;
        out vec3 TangentFragPos;
        
        uniform mat4 uProjection;
        uniform mat4 uView;
        uniform mat4 uModel;

        uniform vec3 lightPos;
        uniform vec3 viewPos;

        void main() {
            fragPos = aPosition;
            texCoords = aTexCoords;
            
            mat3 normalMatrix = transpose(inverse(mat3(uModel)));
        
            vec3 tangent = cross(aNormal, vec3(0.0f, 1.0f, 0.0f));
            if(length(tangent) == 0.0f) tangent = cross(aNormal, vec3(0.0f, 0.0f, 1.0f));
            vec3 T = normalize(normalMatrix * tangent);
            vec3 N = normalize(normalMatrix * aNormal);
            T = normalize(T - dot(T, N) * N);
            vec3 B = cross(N, T);

            mat3 TBN = transpose(mat3(T, B, N));
            TangentLightPos = TBN * lightPos;
            TangentViewPos = TBN * viewPos;
            TangentFragPos = TBN * fragPos;    

            gl_Position = uProjection * uView * uModel * vec4(aPosition, 1.0);
        }
    </script>

    <script src="../shaders/main.frag" id="mainFragShader" type="fragShader">
        #version 300 es
        precision mediump float;

        in vec3 fragPos;
        in vec2 texCoords;
        in vec3 TangentLightPos;
        in vec3 TangentViewPos;
        in vec3 TangentFragPos;

        uniform sampler2D uAlbedo;
        uniform sampler2D uNormal;
        uniform sampler2D uRough;
        uniform sampler2D uMetal;

        out vec4 finalColor;

        float DistributionGGX(vec3 N, vec3 H, float roughness) {
            float a = roughness * roughness;
            float a2 = a * a;
            float NdotH = max(dot(N, H), 0.0f);
            float NdotH2 = NdotH * NdotH;

            float nom = a2;
            float denom = (NdotH2 * (a2 - 1.0f) + 1.0f);
            denom = 3.14159265f * denom * denom;

            return nom / denom;
        }

        float GeometrySchlickGGX(float NdotV, float roughness) {
            float r = (roughness + 1.0f);
            float k = (r * r) / 8.0f;

            float num = NdotV;
            float denom = NdotV * (1.0f - k) + k;

            return num / denom;
        }

        float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness) {
            float NdotV = max(dot(N, V), 0.0f);
            float NdotL = max(dot(N, L), 0.0f);
            float ggx2 = GeometrySchlickGGX(NdotV, roughness);
            float ggx1 = GeometrySchlickGGX(NdotL, roughness);

            return ggx1 * ggx2;
        }

        vec3 FresnelSchlick(float cosTheta, vec3 F0) {
            return F0 + (1.0f - F0) * pow(1.0 - cosTheta, 5.0);
        }

        void main() {
            vec3 albedo = pow(texture(uAlbedo, texCoords).rgb, vec3(2.2f));
            vec3 normal = texture(uNormal, texCoords).rgb;
            float rough = texture(uRough, texCoords).r;
            float metal = texture(uMetal, texCoords).r;

            vec3 N = normalize(normal * 2.0 - 1.0);

            vec3 F0 = vec3(0.04f);
            F0 = mix(F0, albedo, metal);

            vec3 V = normalize(TangentViewPos - TangentFragPos);
            vec3 L = normalize(TangentLightPos - TangentFragPos);
            vec3 H = normalize(V + L);

            float dist = 0.05f * length(TangentLightPos - TangentFragPos);
            float attenuation = 1.0f / (dist * dist);
            vec3 radiance = vec3(1.0f, 1.0f, 1.0f) * attenuation;
       
            float NDF = DistributionGGX(N, H, rough);
            float G = GeometrySmith(N, V, L, rough);
            vec3 F = FresnelSchlick(max(dot(H, V), 0.1f), F0);    

            vec3 kS = F;
            vec3 kD = vec3(1.0f) - kS;
            kD *= 1.0f - metal;
            
            vec3 numerator = NDF * G * F;
            float denominator = 4.0f * max(dot(N, V), 0.0f) * max(dot(N, L), 0.0f);
            vec3 specular = numerator / max(denominator, 0.001f);

            float NdotL = max(dot(N, L), 0.0f);
            vec3 Lo = vec3(0.0f);

            Lo += ((kD) * albedo / 3.14159265f + specular) * radiance * NdotL;
            
            vec3 ambient = vec3(0.03f) * albedo;

            vec3 color = ambient + Lo;
            color = color / (color + vec3(1.0f));
            color = pow(color, vec3(1.0f / 2.2f));
            finalColor = vec4(color, 1.0f);
        }
    </script>
</body>
</html>